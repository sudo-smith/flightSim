#version 450 core

#define MAX_GEO_PER_AIR 3



layout (local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

// Types -----------------------------------------------------------------------

struct GeoPixel {
    vec4 worldPos;
    vec4 normal;
    ivec4 texCoord;
};

struct AirPixel {
    vec4 worldPos;
    vec4 velocity;
    vec4 backForce;
};

struct AirGeoMapElement {
    int geoCount;
    int geoIndices[MAX_GEO_PER_AIR];
};

// Constants -------------------------------------------------------------------

const int k_maxGeoPixels = 32768;
const int k_maxAirPixels = 32768;

const int k_invocCount = 1024;

const float k_maxSearchDist = 0.2f; // In world space
const float k_turbulenceDist = 0.1f;

const bool k_doLineSearch = true;

const bool k_distinguishActivePixels = true; // Makes certain "active" pixels brigher for visual clarity, but lowers performance
const float k_inactiveVal = k_distinguishActivePixels ? 1.0f / 3.0f : 1.0f;

const bool k_ignoreEdges = false;


// Uniforms --------------------------------------------------------------------

layout (binding = 0, rgba8) uniform image2D u_fboImg;
layout (binding = 3, r32i) uniform iimage2D u_flagImg;
layout (binding = 4, rgba8) uniform image2D u_sideImg;

layout (binding = 0, std430) restrict buffer SSBO {
    int u_swap;
    int u_geoCount;
    coherent int u_airCount[2]; // TODO: does this need to be coherent?
    ivec2 u_screenSize;
    vec2 u_screenAspectFactor;
    float u_sliceSize;
    float u_windSpeed;
    float u_dt;
    uint u_debug;
    ivec4 u_momentum;
    vec4 u_force;
    ivec4 u_dragForce;
    ivec4 u_dragMomentum;
};

// Done this way because having a lot of large static sized arrays makes shader compilation super slow for some reason
layout (binding = 1, std430) buffer GeoPixels { // TODO: should be restrict?
    GeoPixel u_geoPixels[];
};
layout (binding = 2, std430) buffer AirPixels { // TODO: should be restrict?
    AirPixel u_airPixels[];
};
layout (binding = 3, std430) buffer AirGeoMap { // TODO: should be restrict?
    coherent AirGeoMapElement u_airGeoMap[]; // TODO: does this need to be coherent?
};

// Functions -------------------------------------------------------------------

vec2 worldToScreen(vec3 world) {
    vec2 screenPos = world.xy;
    screenPos *= u_screenAspectFactor; // compensate for aspect ratio
    screenPos = screenPos * 0.5f + 0.5f; // center
    screenPos *= vec2(u_screenSize); // scale to texture space
    return screenPos;
}

float screenToWorldDist(float screenDist) {
    return screenDist / (float(min(u_screenSize.x, u_screenSize.y)) * 0.5f);
}

void main() {
    int invocI = int(gl_GlobalInvocationID.x);
    int invocWorkload = (u_geoCount + k_invocCount - 1) / k_invocCount;    
    for (int ii = 0; ii < invocWorkload; ++ii) {
    
        int geoI = invocI + (k_invocCount * ii);
        if (geoI >= u_geoCount) {
            return;
        }

        vec3 geoWorldPos = u_geoPixels[geoI].worldPos.xyz;
        vec3 geoNormal = u_geoPixels[geoI].normal.xyz;
        ivec2 geoTexCoord = u_geoPixels[geoI].texCoord.xy; // Need exact texture coord because rasterization math and our world-to-screen math don't always align

        // TODO: magic numbers
        bool shouldSpawn = geoNormal.z >= 0.01f && geoNormal.z <= 0.99f;

        // Look for existing air pixel
        vec2 screenPos = vec2(geoTexCoord) + 0.5f;
        ivec2 pixel = ivec2(screenPos);
        vec2 screenDir = normalize(geoNormal.xy);
        vec2 corner; if (k_doLineSearch) corner = step(vec2(0.0f), screenDir);
        float totalDist = 0.0f;
        while (true) {            
            if (k_doLineSearch) {
                // TODO: optimize
                vec2 delta = corner - (screenPos - vec2(pixel));
                vec2 dist = abs(delta / screenDir);
                if (dist.x < dist.y) {
                    screenPos += screenDir * dist.x;
                    pixel.x += int(sign(screenDir.x));
                    totalDist += screenToWorldDist(dist.x);
                }
                else {
                    screenPos += screenDir * dist.y;
                    pixel.y += int(sign(screenDir.y));
                    totalDist += screenToWorldDist(dist.y);
                }
            }
            else {
                screenPos += screenDir;
                pixel = ivec2(screenPos);
                totalDist += screenToWorldDist(1.0f);
            }

            if (totalDist > k_maxSearchDist) {
                break;
            }

            vec4 color = imageLoad(u_fboImg, pixel);

            // Visualize the search
            if (bool(u_debug)) {
                //imageStore(u_fboImg, pixel, vec4(color.r, color.g, 0.5f, color.a));
            }

            if (color.r != 0.0f) { // we found a geo pixel
                break;
            }
            if (color.g != 0.0f) { // we found an air pixel
                int airI = imageLoad(u_flagImg, pixel).x;               
                if (airI != 0) { // TODO: this should not be necessary, just here for sanity
                    --airI;

                    int mapI = atomicAdd(u_airGeoMap[airI].geoCount, 1);
                    if (mapI >= MAX_GEO_PER_AIR) {
                        break;
                    }
                    u_airGeoMap[airI].geoIndices[mapI] = geoI;

                    shouldSpawn = false;   
                    break;
                }
            }
        }

        // Overwrite flag image with geometry index
        imageStore(u_flagImg, geoTexCoord, ivec4(geoI + 1, 0, 0, 0));
    
        // Make a new air pixel
        if (shouldSpawn) {
            int airI = atomicAdd(u_airCount[u_swap], 1);
            if (airI >= k_maxAirPixels) {
                continue;
            }

            u_airGeoMap[airI].geoCount = 1;
            u_airGeoMap[airI].geoIndices[0] = geoI;

            vec3 airWorldPos = geoWorldPos;
            vec3 refl = reflect(vec3(0.0f, 0.0f, -1.0f), geoNormal);
            vec3 airVelocity = refl * u_windSpeed;

            u_airPixels[airI + u_swap * k_maxAirPixels].worldPos = vec4(airWorldPos, 0.0f);
            u_airPixels[airI + u_swap * k_maxAirPixels].velocity = vec4(airVelocity, 0.0f);
            u_airPixels[airI + u_swap * k_maxAirPixels].backForce = vec4(0.0f);

            // Draw to fbo to avoid another draw later, only necessary for seeing the results immediately
            if (bool(u_debug)) {
                vec4 color = imageLoad(u_fboImg, geoTexCoord);
                color.g = k_inactiveVal;
                imageStore(u_fboImg, geoTexCoord, color);
            }
        }

        // Color active geo pixels more brightly
        if (bool(u_debug) && k_distinguishActivePixels) {
            vec4 color = imageLoad(u_fboImg, geoTexCoord);
            color.r = 1.0f;
            imageStore(u_fboImg, geoTexCoord, color);

            if (!k_ignoreEdges || abs(geoWorldPos.x) <= 0.5f) {
                ivec2 sideTexCoord = ivec2(worldToScreen(vec3(-geoWorldPos.z - 1.0f, geoWorldPos.y, 0)));
                sideTexCoord.y = geoTexCoord.y;
                color = imageLoad(u_sideImg, sideTexCoord);
                color.r = 1.0f;
                imageStore(u_sideImg, sideTexCoord, color);
            }
        }
    }
}