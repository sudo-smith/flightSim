#version 450 core

#define MAX_GEO_PER_AIR 3



layout (local_size_x = 1024, local_size_y = 1, local_size_z = 1) in;

// Types -----------------------------------------------------------------------

struct GeoPixel {
    vec4 worldPos;
    vec4 normal;
    ivec4 texCoord;
};

struct AirPixel {
    vec4 worldPos;
    vec4 velocity;
    vec4 backForce;
};

struct AirGeoMapElement {
    int geoCount;
    int geoIndices[MAX_GEO_PER_AIR];
};

// Constants -------------------------------------------------------------------

const int k_maxGeoPixels = 32768;
const int k_maxAirPixels = 32768;

const bool k_distinguishActivePixels = true; // Makes certain "active" pixels brigher for visual clarity, but lowers performance

const float k_maxSearchDist = 0.2f; // In world space
const float k_turbulenceDist = 0.0225f;

const bool k_doLineSearch = true;

const bool k_ignoreEdges = false;

// Uniforms --------------------------------------------------------------------

layout (binding = 0, rgba8) uniform image2D u_fboImg;
layout (binding = 3, r32i) uniform iimage2D u_flagImg;
layout (binding = 4, rgba8) uniform image2D u_sideImg;

layout (binding = 0, std430) restrict buffer SSBO {
    int u_swap;
    int u_geoCount;
    int u_airCount[2];
    ivec2 u_screenSize;
    vec2 u_screenAspectFactor;
    float u_sliceSize;
    float u_windSpeed;
    float u_dt;
    uint u_debug;
    ivec4 u_momentum;
    vec4 u_force;
    ivec4 u_dragForce;
    ivec4 u_dragMomentum;
};

// Done this way because having a lot of large static sized arrays makes shader compilation super slow for some reason
layout (binding = 1, std430) buffer GeoPixels { // TODO: should be restrict?
    GeoPixel u_geoPixels[];
};
layout (binding = 2, std430) buffer AirPixels { // TODO: should be restrict?
    AirPixel u_airPixels[];
};
layout (binding = 3, std430) buffer AirGeoMap { // TODO: should be restrict?
    AirGeoMapElement u_airGeoMap[];
};

// Shared ----------------------------------------------------------------------

shared vec3 accumulationArray[gl_WorkGroupSize.x];

// Functions -------------------------------------------------------------------

vec2 worldToScreen(vec3 world) {
    vec2 screenPos = world.xy;
    screenPos *= u_screenAspectFactor; // compensate for aspect ratio
    screenPos = screenPos * 0.5f + 0.5f; // center
    screenPos *= vec2(u_screenSize); // scale to texture space
    return screenPos;
}

vec2 screenToWorldDir(vec2 screenDir) {
    return screenDir / (float(min(u_screenSize.x, u_screenSize.y)) * 0.5f);
}

float screenToWorldDist(float screenDist) {
    return screenDist / (float(min(u_screenSize.x, u_screenSize.y)) * 0.5f);
}

void main() {
    int workI = int(gl_LocalInvocationID.x);
    accumulationArray[workI] = vec3(0.0f);

    for (int airI = workI; airI < u_airCount[u_swap]; airI += int(gl_WorkGroupSize.x)) {

        vec3 airWorldPos = u_airPixels[airI + u_swap * k_maxAirPixels].worldPos.xyz;
        vec3 airVelocity = u_airPixels[airI + u_swap * k_maxAirPixels].velocity.xyz;
        float turbulence = u_airPixels[airI + u_swap * k_maxAirPixels].worldPos.w;

        // Calculate backforce
        vec2 backForce = vec2(0.0f);
        float lift = 0.0f;
        float drag = 0.0f;
        
        // If no geo found for this air, search for geo
        if (u_airGeoMap[airI].geoCount == 0) {
            vec2 screenPos = worldToScreen(airWorldPos);
            ivec2 pixel = ivec2(screenPos);
            vec2 searchDir = u_airPixels[airI + u_swap * k_maxAirPixels].backForce.xy;
            if (searchDir != vec2(0.0f)) {                
                vec2 screenDir = normalize(searchDir);
                vec2 corner; if (k_doLineSearch) corner = step(vec2(0.0f), screenDir);
                float totalDist = 0.0f;
                while (true) {            
                    if (k_doLineSearch) {
                        // TODO: optimize
                        vec2 delta = corner - (screenPos - vec2(pixel));
                        vec2 dist = abs(delta / screenDir);
                        if (dist.x < dist.y) {
                            screenPos += screenDir * dist.x;
                            pixel.x += int(sign(screenDir.x));
                            totalDist += screenToWorldDist(dist.x);
                        }
                        else {
                            screenPos += screenDir * dist.y;
                            pixel.y += int(sign(screenDir.y));
                            totalDist += screenToWorldDist(dist.y);
                        }
                    }
                    else {
                        screenPos += screenDir;
                        pixel = ivec2(screenPos);
                        totalDist += screenToWorldDist(1.0f);
                    }

                    if (totalDist > k_maxSearchDist) {
                        break;
                    }

                    vec4 color = imageLoad(u_fboImg, pixel);

                    // Visualize the search
                    if (bool(u_debug)) {
                        //imageStore(u_fboImg, pixel, vec4(color.r, color.g, 0.5f, color.a));
                    }

                    if (color.r != 0.0f) { // we found an geo pixel
                        int geoI = imageLoad(u_flagImg, pixel).x;
                        if (geoI != 0) { // TODO: this should not be necessary, just here for sanity
                            --geoI;

                            ++u_airGeoMap[airI].geoCount;
                            u_airGeoMap[airI].geoIndices[0] = geoI;                   
 
                            break;
                        }
                    }
                }
            }
        }

        // For each associated geo pixel, update backforce, lift, and drag
        // TODO: actually take multiple geo pixels into account
        for (int mapI = 0; mapI < u_airGeoMap[airI].geoCount; ++mapI) {
            int geoI = u_airGeoMap[airI].geoIndices[mapI];

            vec3 geoWorldPos = u_geoPixels[geoI].worldPos.xyz;
            vec3 geoNormal = u_geoPixels[geoI].normal.xyz;
            float dist = distance(airWorldPos.xy, geoWorldPos.xy);
            backForce = normalize(-geoNormal.xy) * dist*dist;
            backForce *= 600.0f;

            if (distance(geoWorldPos, airWorldPos) >= k_turbulenceDist) {
                turbulence = 1.0f;
            }

            // Calculate drag
            float massDensity = 1.0f;
            float flowVelocity = 1.0f;
            float area = screenToWorldDir(vec2(1.0f, 0.0f)).x;
            area = area * area;
            float dragC = 1.0f;
            float dragForce = 0.5f * massDensity * flowVelocity * flowVelocity * dragC * area * max(geoNormal.z, 0.0f);
            dragForce *= 100.0f;
            atomicAdd(u_dragForce.x, int(round(dragForce * 1.0e6f)));
        }

        // Update velocity
        airVelocity.xy += backForce;
        airVelocity.z = -u_windSpeed;
        airVelocity = normalize(airVelocity) * u_windSpeed;

        // Update location
        airWorldPos.xy += airVelocity.xy * u_dt;
        airWorldPos.z -= u_sliceSize;

        u_airPixels[airI + u_swap * k_maxAirPixels].worldPos = vec4(airWorldPos, turbulence);
        u_airPixels[airI + u_swap * k_maxAirPixels].velocity = vec4(airVelocity, 0.0f);
        u_airPixels[airI + u_swap * k_maxAirPixels].backForce = vec4(backForce, 0.0f, 0.0f);
                        
        // Calculate lift     
        vec2 liftForce = -backForce * u_sliceSize * 0.1f;			  
        //atomicAdd(u_force.x, int(round(liftForce * 1.0e6f)));
        accumulationArray[workI].xy += liftForce;

        // Color active air pixels more brightly
        if (bool(u_debug) && k_distinguishActivePixels && u_airGeoMap[airI].geoCount > 0) {
            ivec2 texCoord = ivec2(worldToScreen(airWorldPos));
            vec4 color = imageLoad(u_fboImg, texCoord);
            color.g = 1.0f;
            color.b = turbulence;
            imageStore(u_fboImg, texCoord, color);

            if (!k_ignoreEdges || abs(airWorldPos.x) <= 0.5f) {
                ivec2 sideTexCoord = ivec2(worldToScreen(vec3(-airWorldPos.z - 1.0f, airWorldPos.y, 0)));
                color = imageLoad(u_sideImg, sideTexCoord);
                color.g = 1.0f;
                color.b = turbulence;
                imageStore(u_sideImg, sideTexCoord, color);
            }
        }
    }

    // Add contents of accumulation array
    for (int n = int(gl_WorkGroupSize.x) / 2; n > 0; n /= 2) {
        barrier();
        accumulationArray[workI] += accumulationArray[workI + n];
    }
    if (workI == 0) {
        u_force.xyz = accumulationArray[0];
    }
}